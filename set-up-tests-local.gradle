import com.liferay.workspace.testing.ExecuteAndWaitForTask
import com.liferay.workspace.testing.ServerStartTask
import com.liferay.workspace.testing.ServerStopTask
import com.liferay.workspace.testing.ServerUtil
import com.liferay.workspace.testing.WaitForFileExistsAction

import java.util.concurrent.TimeUnit;

import org.zeroturnaround.process.PidProcess
import org.zeroturnaround.process.ProcessUtil
import org.zeroturnaround.process.Processes

tasks.register("serverStart", ServerStartTask)
tasks.register("serverStop", ServerStopTask)

project(":client-extensions:liferay-sample-etc-node") {
	Project project ->

	project.tasks.register('startMicroservice', ExecuteAndWaitForTask) {
		dependsOn ":serverStart"

		doFirst new WaitForFileExistsAction(provider {"${gradle.liferayWorkspace.homeDir}/routes/default/${project.name}"})

		execArgs.addAll rootProject.file("gradlew").path, "${project.path}:packageRunStart"
		expectedOutput.set "App listening on 3001"

		onlyIf {
			!ServerUtil.isReachable("http://localhost:3001")
		}
	}
}

project(":client-extensions:liferay-sample-etc-spring-boot") {
	Project project ->

	project.tasks.register('startMicroservice', ExecuteAndWaitForTask) {
		dependsOn ":serverStart"

		doFirst new WaitForFileExistsAction(provider {"${gradle.liferayWorkspace.homeDir}/routes/default/${project.name}"})

		execArgs.addAll rootProject.file("gradlew").path, "${project.path}:bootRun"
		expectedOutput.set "Started SampleSpringBootApplication in"

		onlyIf {
			!ServerUtil.isReachable("http://localhost:58081")
		}
	}
}

subprojects {
	Project project ->

	afterEvaluate {
		tasks.withType(ExecuteAndWaitForTask) {
			task ->

			String name = task.name

			String cleanTaskName = "clean" + name.charAt(0).toUpperCase() + name.substring(1)

			tasks.named(cleanTaskName) {
				doFirst {
					if (!task.pid.isPresent()) {
						return
					}

					PidProcess process = Processes.newPidProcess(task.pid.get());

					if (!process.isAlive()) {
						return
					}

					logger.lifecycle("Destroying background process with pid ${task.pid.get()}")

					ProcessUtil.destroyGracefullyOrForcefullyAndWait(process, 30, TimeUnit.SECONDS, 10, TimeUnit.SECONDS)
				}
			}
		}
	}
}

tasks.named("setUp") {
	// frontend CX should be deploying into liferay bundle
	dependsOn ":client-extensions:liferay-sample-custom-element-1:deploy"
	dependsOn ":client-extensions:liferay-sample-custom-element-2:deploy"
	dependsOn ":client-extensions:liferay-sample-etc-node:deploy"
	dependsOn ":client-extensions:liferay-sample-etc-spring-boot:deploy"

	//microservice CX should be started, but they won't start until the routes metadata is available
	dependsOn ":client-extensions:liferay-sample-etc-node:startMicroservice"
	dependsOn ":client-extensions:liferay-sample-etc-spring-boot:startMicroservice"
}

tasks.named("tearDown") {
	dependsOn ":client-extensions:liferay-sample-etc-node:cleanStartMicroservice"
	dependsOn ":client-extensions:liferay-sample-etc-spring-boot:cleanStartMicroservice"
	dependsOn ":serverStop"
}